pluginManagement {
    apply from: 'versions.gradle'
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }
    resolutionStrategy {
        eachPlugin {
            if (requested.id.id == 'dagger.hilt.android.plugin') {
                useModule(deps.hilt.plugin)
            } else if (requested.id.id == 'androidx.navigation.safeargs.kotlin'){
                useModule(deps.navigation.safeArgs)
            }
        }
    }
    plugins {
        id 'com.android.application' version versions.AGP
        id 'com.android.library' version versions.AGP
        id 'org.jetbrains.kotlin.android' version versions.kotlin
        id 'org.jetbrains.kotlin.kapt' version versions.kotlin
        id 'org.jetbrains.kotlin.plugin.parcelize' version versions.kotlin
    }
}
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
    }
}
rootProject.name = "Places"
include ':presentation'
include ':data'
include ':domain'
